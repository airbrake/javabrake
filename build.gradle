buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.11.0"
  }
}

plugins {
    id 'com.jfrog.bintray' version '1.7.3'
}

apply plugin: 'maven-publish'
apply plugin: 'java-library'
apply plugin: 'io.codearte.nexus-staging'

group = 'io.airbrake'
version = '0.2.0'

if (project.hasProperty('signing.keyId')) {
  apply plugin: 'signing'
  signing {
    sign configurations.archives
  }
}

repositories {
    jcenter()
}

dependencies {
    compile 'com.squareup.okhttp3:okhttp:3.8.1'
    compile 'com.google.code.gson:gson:1.7.2'
    compile 'com.google.code.findbugs:jsr305:3.0.2'

    testCompile 'com.github.tomakehurst:wiremock:2.7.1'

    testImplementation 'junit:junit:4.12'
}

test {
    testLogging {
        showStandardStreams = true
        events "passed", "skipped", "failed", "standardOut", "standardError"
        showCauses true
        showExceptions true
        showStackTraces true
        exceptionFormat "full"
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    configurations = ['archives']
    publish = true
    override = true
    pkg {
        repo = 'maven'
        name = project.name
        userOrg = 'airbrake'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/airbrake/javabrake.git'
        version {
            name = project.version
            desc = 'Airbrake notifier for Java'
            vcsTag = project.version
        }
    }
}

if (project.hasProperty('ossrhUsername')) {
  uploadArchives {
    repositories {
      mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
          authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
          authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        pom.project {
          name project.name
          packaging 'jar'
          description 'Airbrake notifier for Java'
          url 'https://airbrake.io'

          scm {
            connection 'https://github.com/airbrake/javabrake.git'
            developerConnection 'https://github.com/airbrake/javabrake.git'
            url 'https://github.com/airbrake/javabrake'
          }

          licenses {
            license {
              name 'MIT License'
              url 'https://opensource.org/licenses/MIT'
            }
          }

          developers {
            developer {
              id 'vmihailenco'
              name 'Vladimir Mihailenco'
              email 'vladimir.webdev@gmail.com'
            }
          }
        }
      }
    }
  }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

task createPom {
    doLast {
        pom {
            project {
                groupId project.group
                artifactId project.name
                version project.version

                inceptionYear '2017'
                licenses {
                    license {
                        name 'MIT License'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }
            }
        }.writeTo("pom.xml")
    }
}
